{
  config,
  lib,
  pkgs,
  inputs,
  ...
}:
let
  inherit (lib) mkEnableOption mkIf;
in
{
  imports = [
    ./hyprlock.nix
  ];

  options.features.wm.hyprland.enable = mkEnableOption "Enable Hyprland window manager";

  config = mkIf config.features.wm.hyprland.enable {
    # Enable Hyprland
    wayland.windowManager.hyprland = {
      enable = true;
      xwayland.enable = true;
      systemd.enable = true;

      # Enable plugins from the hyprland-plugins flake
      # Available plugins: hyprscrolling, hyprtrails, hyprwinwrap, hyprbars, hyprexpo, hyprgrass
      plugins = [
        inputs.hyprland-plugins.packages.${pkgs.stdenv.hostPlatform.system}.hyprscrolling
        # inputs.hyprland-plugins.packages.${pkgs.stdenv.hostPlatform.system}.hyprtrails
        # inputs.hyprland-plugins.packages.${pkgs.stdenv.hostPlatform.system}.hyprwinwrap
        # inputs.hyprland-plugins.packages.${pkgs.stdenv.hostPlatform.system}.hyprbars
        # inputs.hyprland-plugins.packages.${pkgs.stdenv.hostPlatform.system}.hyprexpo
        # inputs.hyprland-plugins.packages.${pkgs.stdenv.hostPlatform.system}.hyprgrass
      ];

      settings = lib.mkMerge [
        (import ./env.nix)
        (import ./general.nix)
        (import ./keybinds.nix)
        (import ./rules.nix)
        (import ./exec.nix)
      ];

      extraConfig = ''
        # Source monitor and workspace configs generated by nwg-displays
        source = ~/.config/hypr/monitors.conf
        source = ~/.config/hypr/workspaces.conf
      '';
    };

    xdg.portal = {
      enable = true;

      extraPortals = with pkgs; [
        xdg-desktop-portal-gtk
      ];

      config = {
        common.default = "*";
        hyprland.default = [
          "hyprland"
          "gtk"
        ];
      };
    };

    features.application = {
      rofi.enable = true;
      waybar.enable = true;
      wlogout.enable = true;
      swaync.enable = true;
    };

    # Essential Hyprland packages
    home.packages = with pkgs; [
      # Wayland essentials
      wayland
      wayland-protocols
      wayland-utils

      # Hyprland ecosystem
      nwg-displays

      # Wallpaper
      swww

      # Screenshot
      grim
      slurp
      grimblast

      # Media control
      playerctl

      # Audio control
      wireplumber
      pavucontrol

      # Brightness control
      brightnessctl

      # Auto-mounting
      udiskie

      # Image viewer
      nsxiv

      # Clipboard manager (optional)
      wl-clipboard
      cliphist
    ];

    # Services
    services = {
      # Auto-mounting
      udiskie = {
        enable = true;
        automount = true;
        notify = true;
        tray = "auto";
      };
    };

    systemd.user.services.waybar = {
      Unit = {
        Description = "Highly customizable Wayland bar for Sway and Wlroots based compositors";
        Documentation = "https://github.com/Alexays/Waybar/wiki";
        PartOf = [ "graphical-session.target" ];
        After = [ "graphical-session.target" ];
        Requires = [ "graphical-session.target" ];
        OnFailure = [ "waybar-failure-notify.service" ];
      };
      Service = {
        Type = "simple";
        ExecStart = "${pkgs.waybar}/bin/waybar";
        ExecReload = "${pkgs.coreutils}/bin/kill -SIGUSR2 $MAINPID";
        Restart = "on-failure";
        RestartSec = "1s";
        Environment = [
          "PATH=${
            pkgs.lib.makeBinPath [
              pkgs.curl
              pkgs.gnused
              pkgs.coreutils
            ]
          }:${config.home.profileDirectory}/bin"
        ];
      };
      Install = {
        WantedBy = [ "graphical-session.target" ];
      };
    };

    # Notification service that triggers on waybar failure
    systemd.user.services.waybar-failure-notify = {
      Unit = {
        Description = "Notify when Waybar crashes";
      };

      Service = {
        Type = "oneshot";
        ExecStart = "${pkgs.libnotify}/bin/notify-send -u critical 'Waybar Crashed' 'Waybar has crashed and been restarted. Check logs: journalctl --user -u waybar -n 50'";
      };
    };
  };
}
